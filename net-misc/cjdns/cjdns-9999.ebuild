# Copyright 1999-2015 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

EGIT_REPO_URI="
		git://github.com/cjdelisle/cjdns.git
		https://github.com/cjdelisle/cjdns.git
"

PYTHON_COMPAT=( python2_7 )
inherit eutils flag-o-matic git-r3 linux-info python-single-r1 toolchain-funcs

DESCRIPTION="Encrypted networking for regular people"
HOMEPAGE="https://github.com/cjdelisle/cjdns"
LICENSE="GPL-3"

SLOT="0"
KEYWORDS=""

# NodeJS is build-time only to prevent it's downloading
DEPEND="${PYTHON_DEPEND}
	>=net-libs/nodejs-0.8.15"
RDEPEND=""

pkg_setup() {
	if ! linux_config_exists; then
		eerror "Unable to check your kernel for TUN support"
	else
		CONFIG_CHECK="~TUN"
		ERROR_TUN="Your kernel lacks TUN support."
	fi
	linux-info_pkg_setup
}

src_prepare() {
	# Do no add -Werror to CFLAGS
	# Respecting CFLAGS is impossible due to CFLAGS variable pollution from bundled deps
	# Unbundling livuv and nodejs is a hard task :-(
	sed -i \
		-e "/'-Werror'/d" \
		-e "/'-g'/d" \
		node_build/make.js || die
	tc-export CC

	epatch_user
}

src_compile() {
	python_setup
	node node_build/make.js || die
}

src_install() {
	newinitd "${FILESDIR}/cjdns.initd" cjdns

	dodoc README.md rfcs/*
	dosbin cjdroute
}

pkg_postinst() {
	local config_file="cjdroute.conf"
	local config_path="${ROOT}etc/${config_file}"

#	if [[ ! -e "${config_path}" ]] ; then
#		ebegin "Generating ${config_file}..."
#		(umask 077 && cjdroute --genconf > "${T}/${config_file}") || die "cjdroute --genconf failed"
#		mv "${T}/${config_file}" "${config_path}"
#		eend ${?} || die "Failed to generate and install ${config_file}"
#		elog "The keys in ${config_path} have been autogenerated during "
#		elog "emerge, they are not defaults and do not need to be overwritten."
#	fi
	ewarn "Protect ${config_path}! A lost conf file means you have "
	ewarn "lost your password and connections and anyone who connected "
	ewarn "to you will no longer be able to connect. A *compromised* "
	ewarn "conf file means that other people can impersonate you on "
	ewarn "the network."
	ewarn
	einfo "The cjdns runscript will load the TUN kernel module automatically."
	einfo "If you are using systemd and have TUN built as a module, add tun "
	einfo "to /etc/modules-load.d/ for automatic loading at boot-time."
	einfo "echo tun > /etc/modules-load.d/cjnds.conf"
}
